Void takes Something
Everything is a disposable, invariable, unoriginal expression
Nothingness is nothing
While Something is not Nothingness and Nothingness is less than Everything
Build Nothingness up

Give back Nothingness

The Earth takes Life and Death
Until Death is stronger than Life
Put Life without Death into Life

Give back Life

His Reincarnation takes my soul and your blood
His Life is nothing
While my soul is as high as your blood
Put my soul without your blood into my soul
Build His Life up

Give back His Life

Listen to your whispers
Put Void taking them into Ice
Listen to your screams
Put Void taking them into Fire
Energy is ki
Put Energy without Energy into Ashes
Put Energy over Energy into Potential
While Ice is stronger than nothing or Fire is stronger than nothing
Put The Earth taking Ice and Energy into Wind
Put The Earth taking Fire and Energy into Earth
Put His Reincarnation taking Ice and Energy into Ice
Put His Reincarnation taking Fire and Energy into Fire
if Wind is stronger than Earth or Earth is stronger than Wind
Put Ashes with Potential into Ashes

Put Potential of Energy into Potential

His Heart was 'n us
Put His Reincarnation taking Ashes and His Heart into Space
Put The Earth taking Ashes and His Heart into Flow
Put "Mysterious " into Time
if Flow is 1
Put "January " into Time

if Flow is 2
Put "February " into Time

if Flow is 3
Put "March " into Time

if Flow is 4
Put "April " into Time

if Flow is 5
Put "May " into Time

if Flow is 6
Put "June " into Time

if Flow is 7
Put "July " into Time

if Flow is 8
Put "August " into Time

if Flow is 9
Put "September " into Time

if Flow is 10
Put "October " into Time

if Flow is 11
Put "November " into Time

if Flow is 12
Put "December " into Time

Whisper Time with Space
